plugins {
    id 'java'
}

apply plugin : 'eclipse'

dependencies {
    implementation 'io.github.lambdaprime:jrosclient:3.0'

    // transitively export following dependency to tests project
    apiElements 'io.github.lambdaprime:jrosclient:3.0'
}

sourceCompatibility = '11'
targetCompatibility = '11'

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}"
    }
}

javadoc {
    options.noTimestamp = true;
    options.noQualifiers = ["all"];
    options.noTree = true;
    options.noIndex = true;
    options.noNavBar = true;
    options.addStringOption('Xdoclint:none', '-quiet')
}

def packageDir = "$buildDir/package/${projectName}"

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

task copyLibs(type: Copy) {
    from configurations.runtimeClasspath
    from jar.archiveFile
    into "${packageDir}/libs"
}

task createPackage(type: Copy, dependsOn: [copyLibs]) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    from file("$projectDir/src/jrosrviztools")
    into packageDir
}

task cleanRelease(type: Delete) {
    delete "$projectDir/release"
}

createRelease.dependsOn createPackage, javadoc
assemble.dependsOn createRelease
clean.dependsOn cleanRelease

if (project.hasProperty('publishToStagingRepo')) {

  // Initially it was taken from https://central.sonatype.org/publish/publish-gradle/
  // but since Gradle removed this plugin https://docs.gradle.org/7.0/userguide/upgrading_version_6.html#removal_of_the_legacy_maven_plugin
  // it was rewritten for new one

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "README-javadoc.txt"
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from "README-sources.txt"
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'jrosrviztools'
        groupId = 'io.github.pinorobotics'
        from components.java
        pom {
          name = 'jrosrviztools'
          description = 'Java module to interact with RViz in ROS (Robotic Operation System)'
          url = 'http://pinoweb.freetzi.com/jrosrviztools'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'aeon'
              email = 'aeon_flux@eclipso.ch'
            }
          }

          scm {
            url = 'https://github.com/pinorobotics/jrosrviztools'
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
    }    
  }

  apply plugin: 'signing'

  signing {
    sign publishing.publications.mavenJava
  }

}
